class System
  elements! Element*
  sensors! Sensor*

class Attachable 
  sensor: Sensor?

class Pipe < Attachable
  input: OutputElement
  output: InputElement?
  diameter: real
  length: real
  // computed
  area: real = diameter * diameter * 3.14/4
  volume: real = area * length

class Element < Attachable
   name# str

class OutputElement < Element
  outputs! Pipe* / input
  output:Pipe = outputs[0]

class InputElement < Element
  inputs: Pipe* / output
  input: Pipe = inputs[0]

class Joint < OutputElement, InputElement 

class Source < OutputElement
  kind: str

class Exhaust < InputElement 

class Burner < OutputElement, InputElement
  gas: Pipe

class Radiator < OutputElement, InputElement

class Room < Element

class Vessel < OutputElement, InputElement
  capacity: real

class Valve < OutputElement, InputElement

class Splitter < InputElement, OutputElement
  left:Pipe = outputs[0]
  right:Pipe = outputs[1]

class Pump < OutputElement, InputElement

class Sensor
  name# str
  kind: str // temperature, pressure, flow
  controllable: bool
  attach:Attachable / sensor

primitive str
primitive int
primitive bool
primitive real
