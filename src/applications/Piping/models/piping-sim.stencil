diagram "Piping Simulation" (system)

graph [font.size=12,fill.color=(255,255,255)] {
  // create the nodes (one for each class)
  for "Element" elem : system.elements
   label elem {
     if (elem is Temperatured)
       oval [line.width=1,fill.color=(250,250-((elem.input.temperature-40)*6),250-((elem.input.temperature-40)*6))] {
         vertical {
           text [font.size=16,font.weight=700] elem.name
           if (elem is UserTemperature)
             text [font.size=10,font.weight=400] elem.user_temp
         }
       }
     if not (elem is Temperatured)
       oval [line.width=1,fill.color=(200,200,200)] {
         text [font.size=16,font.weight=700] elem.name
       }
    }
  // create the links
  for "Input" elem : system.elements
    if (elem is InputElement and not (elem is Joint))
        connector [line.width=3,line.color=(250,250-((elem.input.temperature-40)*6),250-((elem.input.temperature-40)*6))] (elem <-- elem.input.input)
  for elem : system.elements
    if (elem is Joint)
        for "Connection" pipe : elem.inputs
           connector [line.width=3,line.color=(250,250-((elem.input.temperature-40)*6),250-((elem.input.temperature-40)*6))]  (elem <-- pipe.input)

  // create the sensors
  for "Sensor" sensor : system.sensors
    label sensor {
        box {  // this is the class rectangle
          vertical {
           text [font.size=8,font.weight=600] sensor.name
           text [font.size=8,font.weight=400] sensor.kind
           horizontal {
            text [font.size=8,font.weight=400] "current:"
            if sensor.kind == "temperature"
              text [font.size=8,font.weight=400] sensor.attach.temperature
            else if sensor.kind == "pressure"
              text [font.size=8,font.weight=400] sensor.attach.pressure
            else if sensor.kind == "flow"
              text [font.size=8,font.weight=400] sensor.attach.flow
           }
           if sensor.controllable
             horizontal {
              text [font.size=8,font.weight=400] "desired:"
              text+ [font.size=8,font.weight=400] sensor.user
             }
          }
        }
    }

  for sensor : system.sensors
    connector (sensor -- sensor.attach)
}
